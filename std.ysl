%include "core.ysl"

func get_element arr index
	param arr
	param index

	mul $index 2
	to index

	add $arr $index
	to arr
	peek16 $arr
endfunc

func set_element arr index value
	param arr
	param index
	param value

	mul $index 2
	to index

	add $arr $index
	to arr
	poke16 $arr $value
endfunc

func get_char str index
	param str
	param index

	add $str $index
	to str
	peek8 $str
endfunc

func set_char str index value
	param str
	param index
	param value

	add $str $index
	to str
	poke8 $str $value
endfunc

func puthex byte
	param byte
	local int temp
	local array 16 digits

	set_element &digits 0 !0
	set_element &digits 1 !1
	set_element &digits 2 !2
	set_element &digits 3 !3
	set_element &digits 4 !4
	set_element &digits 5 !5
	set_element &digits 6 !6
	set_element &digits 7 !7
	set_element &digits 8 !8
	set_element &digits 9 !9
	set_element &digits 10 !A
	set_element &digits 11 !B
	set_element &digits 12 !C
	set_element &digits 13 !D
	set_element &digits 14 !E
	set_element &digits 15 !F

	div $byte 16
	to temp
	get_element &digits $temp
	to temp
	putch $temp

	mod $byte 16
	to temp
	get_element &digits $temp
	to temp
	putch $temp
endfunc

func putstr str
	param str

	local int index
	local int ch
	set index 0
	get_char $str $index
	to ch

	while greater $ch 0
		putch $ch
		add $index 1
		to index
		get_char $str $index
		to ch
	endwhile
endfunc

func strcmp s1 s2
	param s1
	param s2

	local int index
	local int ch1
	local int ch2
	set index 0
	get_char $s1 $index
	to ch1
	get_char $s2 $index
	to ch2

	while greater $ch1 0
		if equals $ch1 $ch2
			# bla bla bla
		else
			return 0
		endif

		add $index 1
		to index
		get_char $s1 $index
		to ch1
		get_char $s2 $index
		to ch2
	endwhile

	return 1
endfunc

func readstr out
	param out

	local int input
	local int index

	while equals 1 1
		getch
		to input
		putch $input

		if equals $input 13
			putch 10
			return
		endif

		set_char $out $index $input
		add $index 1
		to index
	endwhile
endfunc

func max n1 n2
	param n1
	param n2

	if greater $n1 $n2
		return $n1
	else
		return $n2
	endif
endfunc

func min n1 n2
	param n1
	param n2

	if less $n1 $n2
		return $n1
	else
		return $n2
	endif
endfunc

func memcpy src dest n
	param src
	param dest
	param n

	local int i

 	for i $n
		local int byte
		peek8 $src
		to byte
		poke8 $dest $byte

		add $src 1
		to src
		add $dest 1
		to dest
	endfor
endfunc

func strlen str
	param str

	local int idx
	local int character

	get_char &str $idx
	to character

	while greater $character 0
		add $idx 1
		to idx
		get_char &str $idx
		to character
	endwhile

	add $idx 1
	to idx

	return $idx
endfunc

func newline
	putch 13
	putch 10
endfunc

# following functions made by @LordOfTrident
func not_equals n1 n2
	param n1
	param n2

	if equals $n1 $n2
		return 0
	else
		return 1
	endif
endfunc

func var_add address value
	param address
	param value

	local int number
	peek16 $address
	to number
	add $number $value
	to number
	poke16 $address $number
endfunc

func var_sub address value
	param address
	param value

	local int number
	peek16 $address
	to number
	sub $number $value
	to number
	poke16 $address $number
endfunc

func var_mul address value
	param address
	param value

	local int number
	peek16 $address
	to number
	mul $number $value
	to number
	poke16 $address $number
endfunc

func var_div address value
	param address
	param value

	local int number
	peek16 $address
	to number
	div $number $value
	to number
	poke16 $address $number
endfunc

func inc address
	param address

	var_add $address 1
endfunc

func dec address
	param address

	var_sub $address 1
endfunc

func putdec num
	param num

	if equals $num 0
		putch !0
		return
	endif

	local string digits "00000"
	local int address
	local int pos
	local int i
	set i 0

	set address &digits

	while not_equals $num 0
		sub 4 $i
		to pos

		local int offset
		mod $num 10
		to offset

		local int ch
		set ch !0

		var_add &ch $offset
		set_char &digits $pos $ch

		var_div &num 10
		if equals $num 0
			var_add &address $pos
		endif

		inc &i
	endwhile

	putstr $address
endfunc
